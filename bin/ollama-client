#!/usr/bin/env raku
use v6.d;

use WWW::Ollama::Client;
use JSON::Fast;

my %*SUB-MAIN-OPTS = :named-anywhere;

#| Ollama client invocation.
sub MAIN(
        *@words,
        Str :$path = 'completion',          #= Path, one of 'completion', 'chat', 'embedding', 'model-info', 'list-models', or 'list-running-models'.
        Str :$model = 'gemma3:1b',          #= Model to use.
         ) {

    my $ollama = WWW::Ollama::Client.new;

    given $path {
        when $_ ∈ <list-models models> {
            my $ans = |$ollama.list-models;
            say to-json($ans, :pretty);
        }
        when $_ ∈ <model-info> {
            my $ans = $ollama.model-info(:$model);
            say to-json($ans, :pretty);
        }
        when $_ ∈ <completion generation> {
            my %body = :$model, prompt => @words.join(' '), :!stream;
            my $ans = $ollama.completion(%body);
            say to-json($ans, :pretty);
        }
        when $_ ∈ <chat chat-completion> {
            # TODO Check for valid JSON message input and/or JSON file.
            my %body = :$model,
                       messages => [{role => "user", content => "How many people live in Brazil?"},];
            my $ans = $ollama.chat(%body);
            say to-json($ans, :pretty);
        }
        default {
            die 'Do not know how to process the --path argument.'
        }
    }
}
